background_image = Image("background.png");

line_number = 10;

progress_image = Image("progress.png");

logo_image = Image("logo.png");
logo_scale = 0.15;
logo_speed = 0.08;

busy_image = Image("busy.png");
busy_scale = 0.25;
busy_speed = 0.2;

half_screen_width = (screen_width = Window.GetWidth()) / 2;
half_screen_height = (screen_height = Window.GetHeight()) / 2;

if (screen_width / screen_height >
    background_image.GetWidth() / background_image.GetHeight())
    background_image = background_image.Scale(screen_width,
                                              background_image.GetHeight() /
                                              background_image.GetWidth() *
                                              screen_width);
else background_image = background_image.Scale(background_image.GetWidth() /
                                               background_image.GetHeight() *
                                               screen_height,
                                               screen_height);

(background_sprite = Sprite()).SetPosition(half_screen_width -
                                           background_image.GetWidth() / 2,
                                           half_screen_height -
                                           background_image.GetHeight() / 2, 0);

background_sprite.SetImage(background_image);

if (screen_width > screen_height) {
    logo_image = logo_image.Scale(logo_image.GetWidth() /
                                  logo_image.GetHeight() *
                                  screen_height * logo_scale,
                                  screen_height * logo_scale);
    busy_image = busy_image.Scale(busy_image.GetWidth() /
                                  busy_image.GetHeight() *
                                  screen_height * busy_scale,
                                  screen_height * busy_scale);
}
else {
    logo_image = logo_image.Scale(screen_width * logo_scale,
                                  logo_image.GetHeight() /
                                  logo_image.GetWidth() *
                                  screen_width * logo_scale);
    busy_image = busy_image.Scale(screen_width * busy_scale,
                                  busy_image.GetHeight() /
                                  busy_image.GetWidth() *
                                  screen_width * busy_scale);
}

logo_sprite = Sprite();
logo_sprite.SetPosition(half_screen_width - logo_image.GetWidth() / 2,
                        half_screen_height - logo_image.GetHeight() / 2, 4);

busy_sprite = Sprite();
busy_sprite.SetPosition(half_screen_width - busy_image.GetWidth() / 2,
                        half_screen_height - busy_image.GetHeight() / 2, 5);

busy_rotation = 0;

if (Plymouth.GetMode() == "boot") {
    for (i = 0; i < line_number; i++) {
        (text_sprite[i] = Sprite()).SetOpacity((i + 1) / line_number);
        text_sprite[i].SetImage(Image.Text(""));
    }

    Plymouth.SetUpdateStatusFunction(fun (text) {
        for (i = 0; i < line_number - 1; i++) {
            text_sprite[i].SetImage(text_image = text_sprite[i + 1].GetImage());
            text_sprite[i].SetPosition(half_screen_width -
                                       text_image.GetWidth() / 2,
                                       screen_height -
                                       (line_number - i) * 18, 2);
        }
        text_sprite[i].SetImage(text_image = Image.Text(text,
                                                        0.41, 0, 0.89));
        text_sprite[i].SetPosition(half_screen_width -
                                   text_image.GetWidth() / 2,
                                   screen_height -
                                   (line_number - i) * 18, 2);
    });

    (progress_sprite = Sprite()).SetPosition(-1 * screen_width, 0, 3);
    progress_sprite.SetImage(progress_image.Scale(screen_width,
                                                  progress_image.GetHeight()));

    Plymouth.SetBootProgressFunction(fun (time, progress) {
        progress_sprite.SetPosition(-1 * screen_width * (1 - progress), 0, 3);
    });

    logo_sprite.SetOpacity(logo_opacity = 0);
    logo_sprite.SetImage(logo_image);

    Plymouth.SetRefreshFunction(fun () {
        if (logo_opacity < 1) {
            if ((logo_opacity += logo_speed) > 1) logo_opacity = 1;
            logo_sprite.SetOpacity(logo_opacity);
        }
        busy_sprite.SetImage(busy_image.Rotate(busy_rotation += busy_speed));
    });
}
else if (Plymouth.GetMode() == "shutdown") {
    logo_opacity = 1;
    logo_sprite.SetImage(logo_image);

    Plymouth.SetRefreshFunction(fun () {
        if (logo_opacity > 0) {
            if ((logo_opacity -= logo_speed) < 0) logo_opacity = 0;
            logo_sprite.SetOpacity(logo_opacity);
        }
        busy_sprite.SetImage(busy_image.Rotate(busy_rotation += busy_speed));
    });
}
else {
    logo_sprite.SetImage(logo_image);

    Plymouth.SetRefreshFunction(fun () {
        busy_sprite.SetImage(busy_image.Rotate(busy_rotation += busy_speed));
    });
}
